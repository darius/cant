;; An example from William Cook's essay on OOP vs. ADTs
;; http://www.cs.utexas.edu/~wcook/Drafts/2009/essay.pdf
;; Sets of integers, in an OO style.

(make empty-set
  (to ~.empty?      #yes)
  (to (~ .has? _)   #no)
  (to (~ .adjoin k) (adjoin<- k empty-set))
  (to (~ .merge s)  s))

(to (adjoin<- n s)
  (if (s .has? n)
      s
      (make extension
        (to ~.empty?      #no)
        (to (~ .has? k)   (or (= n k) (s .has? k)))
        (to (~ .adjoin k) (adjoin<- k extension))
        (to (~ .merge s)  (merge<- extension s)))))

(to (merge<- s1 s2)
  (make meld
    (to ~.empty?      (and s1.empty? s2.empty?))
    (to (~ .has? k)   (or (s1 .has? k) (s2 .has? k)))
    (to (~ .adjoin k) (adjoin<- k meld))
    (to (~ .merge s)  (merge<- meld s))))

;; Smoke test

(hide
 (let eg ((empty-set .adjoin 6) .adjoin 5))

 (out .print (eg .has? 5))
 (out .print (eg .has? 6))
 (out .print (eg .has? 7))
)
