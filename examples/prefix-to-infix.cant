;; Format an infix expression into a text with minimal parentheses.

;; Glossary:
;; p: Precedence level. Higher-precedence operators bind more tightly.
;; e: Expression in s-expr form.

(to (format-infix e)
  (fmt 0 e))

(to (fmt p e)
  (may e
    (be `(,op ,x ,y)
      (fmt-binary p op x y))
    (be `(,(? associative? op) ,@xs)
      (fmt p (foldl1 (on (x y) `(,op ,x ,y)) xs)))
    (be `(,op ,x)
      (fmt-unary p op x))
    (else
      ("~w" .format e))))

(to (associative? op)
  ('(+ *) .find? op))

(to (fmt-unary p op x)
  (may (unaries .get op)
    (be #no          ("~w(~d)" .format op (fmt 0 x)))
    (be {prefix pp}  (enclose p pp ("~w~d" .format op (fmt pp x))))
    (be {postfix pp} (enclose p pp ("~d~w" .format (fmt pp x) op)))))

(to (fmt-binary p op x y)
  (may (binaries .get op)
    (be #no
      ("~w(~d, ~d)" .format op (fmt 0 x) (fmt 0 y)))
    (be {binary left right}
      (enclose p left ("~d ~w ~d" .format (fmt left x) op (fmt right y))))))

(to (enclose outer inner text)
   (if (<= outer inner)
       text
       ("(~d)" .format text)))

(let unaries (!map<-))
(let binaries (!map<-))

(to (def-prefix name p)  (unaries .set! name {prefix p}))
(to (def-postfix name p) (unaries .set! name {postfix p}))

(to (def-infix name p)   (binaries .set! name {binary p p.+}))

(def-prefix '- 10)                      ;XXX get the right precedences
(def-postfix '! 100)

(def-infix '+ 10)
(def-infix '- 10)
(def-infix '* 20)
(def-infix '/ 20)
(binaries .set! '^ {binary 30 30})   ; right-to-left associativity (is this correct?)

;; smoke test

(to (try pexp)
  (out .say "~w -> ~d\n" pexp (format-infix pexp)))

(try '(* (+ 1 2) (+ (* 3 4) (* 5 6))))
(try '(* (+ (- 1) 2)
         (+ (* 3 (! 4))
            (* 5 6))))
(try '(! 4))
(try '(! (- 4)))
(try '(* 3 (! 4)))
(try '(* (! 4) 3))
(try '(* 3 1 4))
