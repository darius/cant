;; a la The Little Prover
;; * I think it'd be easier to understand a J-Bob written in stages:
;;   - Just parsing, checking well-formedness.
;;   - Only reason about if-expressions and equality. (I'm not sure if this piece breaks off cleanly.)
;;   - Prove theorems about straight-line code using operators and defuns.
;;   [we are here]

(import (use 'pretty-print) pp<-)       ;XXX for debugging
(let pp (pp<- (export out)))

;; N.B. specialized to args and result all being expressions
(to (trace fn @args)
  (when loud? (pp `(>>> ,fn ,@(each unparse-e args))))
  (let result (fn @args))
  (when loud? (pp `(<<< ,fn : ,(unparse-e result))))
  result)

(let loud? #no)


;; Types:

;; proof:   {proof def steps}

;; def:     {def name formals meaning}
;; meaning: {fun e}
;;          {thm e}
;;          {op fn}    "rator?" in J-Bob

;; e:       {constant datum}
;;          {variable name}
;;          {if e e e}
;;          {call name es}


;; Top level.

(to (J-Bob/define defs x-proofs)
  (bob/define defs
              (each parse-proof x-proofs)))

(to (bob/define defs proofs)
  (if (valid? defs proofs)
      (rewrite/define+ defs proofs)
      defs))

(to (bob/prove defs proofs)
  (if (valid? defs proofs)
      (rewrite/prove+ defs proofs)
      no-c))

(to (valid? defs proofs)
  (and (defs? '() defs)
       (proofs? defs proofs)))

(to (bob/step defs e steps)
  (if (and (defs? '() defs)
           (expr? defs 'any e)
           (steps? defs steps))
      (rewrite/steps defs e steps)
      e))


;; TODO explain me
;; TODO did I refactor duplication correctly?
;;   Seems unlikely since it got so much shorter.

(to (rewrite/define+ defs proofs)
  (may proofs
    (be '()
      defs)
    (be `({proof ,def ,steps} ,@rest-proofs)
      (if (= yes-c (rewrite/prove defs def steps))
          (rewrite/define+ (append defs def) rest-proofs)
          #no))))                        ;was defs in book


;; TODO explain me

(to (rewrite/prove+ defs proofs)
  (may proofs
    (be '()
      yes-c)
    (be `({proof ,def ,steps} ,@rest-proofs)
      (let e (rewrite/prove+ (append defs def) rest-proofs))
      (if (= e yes-c)
          (rewrite/prove defs def steps) ;y'know, if we did this first it'd be more like rewrite/define+
          e))))

(to (rewrite/prove defs def steps)
  (let {def _ formals meaning} def)
  (may meaning
    (be {fun _}    yes-c)
    (be {thm body} (rewrite/steps defs body steps)))) ;XXX should be using the formals too


;; Rewriting... TODO explain me

(to (rewrite/steps defs claim steps)
  (may steps
    (be '()
      claim)
    (be `(,first ,@rest)
      (let new (rewrite/step defs claim first))
      (if (= new claim)
          new
          (rewrite/steps defs new rest)))))

(to (rewrite/step defs claim {step path app})
  (let {call f _} app)
  (equality/def claim path app (lookup f defs)))


;; Equality... TODO explain me
;; N.B. their 'claim' is not a Cant claim type

;; Pre: the app is a call with same name as the def.
(to (equality/def claim path app {def name formals meaning})
  (when loud?
    (pp `(equality/def ,(unparse-e claim) ,path ,(unparse-e app) ,(unparse-def {def name formals meaning}))))
  (let {call _ args} app)
  (let result
    (equality/path claim path
                   (may meaning
                     ;; TODO do these constructed (= foo bar) exprs ever cause rewriting-in-reverse?
                     ;;   Or is this just a fancy way of substituting (eval-op app), etc.?
                     (be {op fn}    `{call = (,app ,(eval-op fn args))})
                     (be {fun body} (sub-e formals args
                                        `{call = ({call ,name ,(express-variables formals)}
                                                  ,body)}))
                     (be {thm body} (sub-e formals args body)))))
  (when loud? (pp `(equality/def : ,(unparse-e result))))
  result)

(to (express-variables names)              ;TODO this is clumsy
  (for each ((name names))
    {variable name}))

;; If thm after premise-snipping is an equality applying to e at path,
;; then apply it; else leave e alone.

;; N.B. despite the name, thm is syntactically an expression.

;; For example,
;;   (let e    (parse-e '(if foo 1 apple)))
;;   (let path '(E))
;;   (let thm  (parse-e '(if foo #no (= banana apple))))
;; but not
;;   (let thm  (parse-e '(if BAR #no (= banana apple))))
;; Yields
;;   (unparse-e (equality/path e path thm)) => (if foo '1 banana)

;; Fancier example:
;;   (let e    (parse-e '(if bar 2 (if foo 1 apple))))
;;   (let path '(E E))
;;   (let thm  (parse-e '(if foo #no (= banana apple))))
;; Yields
;;   (unparse-e (equality/path e path thm)) => (if bar '2 (if foo '1 banana))

(to (equality/path e path thm)
  (if (has-path? e path)
      (set-at-path e path
                   (trace equality/equation (get-at-path e path)
                                            (snip-premises e path thm)))
      e))

;; As far as possible, remove any top-level if-then-elses from thm.
;; This is possible when the if-question appears also as the question
;; of some 'if' in path through e1; then dig out the branch of thm's
;; 'if' corresponding to the next branch of the path through e1's 'if'.
(to (snip-premises e path thm)
  (may thm
    (be {if q _ _}
      (may (find-premise e path q)
        (be #no thm)
        (be dir (snip-premises e path (get-at-direction thm dir)))))
    (else
      thm)))

;; Seek an `if` in e, somewhere along the path, having a question
;; equal to premise. If found, return the dir of the path's next step
;; from there -- i.e. the A or E of the if having that premise.
(to (find-premise e path premise)
  (begin matching ((e e) (path path))
    (and path.some?
         (or (and ('(A E) .find? path.first)
                  (may e
                    (be {if q _ _} (and (= q premise) path.first))
                    (else          #no)))
             (matching (get-at-direction e path.first) path.rest)))))

;; Rewrite focus according to concl-inst if concl-inst is an equality.
(to (equality/equation focus concl-inst)
  (may concl-inst
    (be `{call = (,arg1 ,arg2)} (equality focus arg1 arg2))
    (else                       focus)))

;; Taking "a=b" as a rewrite rule, rewrite focus if possible.
(to (equality focus a b)
  (hm (if (= focus a) b)
      (if (= focus b) a)
      (else           focus)))


;; Substitute vars -> args in an expression.

(to (sub-e vars args e)
  (let subst (map<-items (zip vars args)))
  (begin subbing ((e e))
    (may e
      (be {constant _}    e)
      (be {variable name} (subst .get name e))
      (be {if q a e}      {if (subbing q)
                              (subbing a)
                              (subbing e)})
      (be {call f es}     {call f (each subbing es)}))))


;; Check okayness of proofs.
;; Each purports to prove a defun or dethm in the context of defs plus
;; the defs of the preceding proofs.
;; A proof is okay when its def and steps are all okay.

(to (proofs? defs proofs)
  (or proofs.none?
      (and (proof? defs proofs.first)
           (do (let {proof def _} proofs.first)
               (proofs? (append defs def) proofs.rest)))))

(to (proof? defs {proof def steps})
  (and (def? defs def)
       (steps? defs steps)))


;; Check okayness of defs.
;; Each must be okay in the context of just the known and preceding
;; defs. A dethm/defun is okay when it's being defined for the first
;; time and its body is okay. Recursion is not allowed,

(to (defs? known-defs defs)
  (or defs.none?
      (and (def? known-defs defs.first)
           (defs? (append known-defs defs.first) defs.rest))))

(to (def? known-defs {def name formals meaning})
  (and (undefined? name known-defs)
       (may meaning
         (be {thm body} (expr? known-defs formals body))
         (be {fun body} (expr? known-defs formals body))
         (be {op _}     #yes))))


;; Check okayness of steps.
;; Each step's call must name a def with the correct arity;
;; if it's an operator, the arguments must be constants;
;; else they must be okay expressions.

(to (steps? defs steps)
  (for every ((step steps))
    (let {step _ {call name args}} step)
    (let {def _ formals meaning} (lookup name defs))
    (and (arity? formals args)
         (may meaning
           (be {op _} (every constant? args))
           (else      (exprs? defs 'any args)))))) ;XXX so where do we check the boundness of args?


;; Check okayness of expressions.
;; Variables must be bound according to vars;
;; calls must name a defun or operator with the correct arity.

(to (expr? defs vars e)
  (may e
    (be {constant _}    #yes)
    (be {variable name} (bound? name vars))
    (be {if q a e}      (exprs? defs vars `(,q ,a ,e)))
    (be {call _ es}     (and (call-arity? defs e)
                             (exprs? defs vars es)))))

(to (exprs? defs vars es)
  (for every ((e es))
    (expr? defs vars e)))

(to (call-arity? defs {call name args})
  (may (lookup name defs)
    (be {def _ formals {op _}}  (arity? formals args))
    (be {def _ formals {fun _}} (arity? formals args))
    (else                       #no))) ;TODO maybe assume this case never happens. Then just ignore the meaning field.

(to (arity? formals args)
  (= formals.count args.count))


;; Name lookup.

(to (lookup name defs)
  (may defs
    (be '()
      #no)                ;N.B. original returns name instead
    (be `(,def ,@rest)
      (let {def dname _ _} def)
      (if (= name dname)
          def
          (lookup name rest)))))

(to (undefined? name defs)
  (not (lookup name defs)))

(to (bound? var vars)
  (or (= vars 'any)
      (vars .find? var)))


;; Rewriting.
;; Is there some lensy way this could be shorter?
;; Well, get-at-path is only used together with set-at-path, so maybe we could combine them...

(to (get-at-path e path)
  (foldl get-at-direction e path))

(to (set-at-path e1 path e2)
  (begin walking ((e1 e1) (path path))
    (if path.none?
        e2
        (set-at-direction e1 path.first
                          (walking (get-at-direction e1 path.first)
                                   path.rest)))))

(to (get-at-direction e dir)
  (may dir
    (be 'Q       (may e (be {if q _ _}    q)))
    (be 'A       (may e (be {if _ a _}    a)))
    (be 'E       (may e (be {if _ _ e}    e)))
    (be (? nat?) (may e (be {call _ args} (get-arg args dir))))))

(to (set-at-direction e1 dir e2)
  (may dir
    (be 'Q       (may e1 (be {if q a e}    {if e2 a e})))
    (be 'A       (may e1 (be {if q a e}    {if q e2 e})))
    (be 'E       (may e1 (be {if q a e}    {if q a e2})))
    (be (? nat?) (may e1 (be {call f args} {call f (set-arg args dir e2)})))))

(to (get-arg args n)
  (args n.-))

(to (set-arg args n e)
  (if (= n 1)
      (link e args.rest)
      (link args.first (set-arg args.rest n.- e))))


;; Check okayness of a path.

(to (has-path? e path)
  (or path.none?
      (and (has-direction? e path.first)
           (has-path? (get-at-direction e path.first)
                      path.rest))))

(to (has-direction? e dir)
  (may dir
    (be 'Q       (may e (be {if _ _ _} #yes) (else #no)))
    (be 'A       (may e (be {if _ _ _} #yes) (else #no)))
    (be 'E       (may e (be {if _ _ _} #yes) (else #no)))
    (be (? nat?) (may e (be {call _ args} (<= 1 dir args.count))
                        (else             #no)))))


;; Some expression helpers.
;; TODO generic map or fold

(let yes-c {constant #yes})
(let no-c  {constant #no})

(to (constant? e)
  (may e
    (be {constant _} #yes)
    (else            #no)))

(to (variable? e)
  (may e
    (be {variable _} #yes)
    (else            #no)))


;; Apply an operator to values.

(to (eval-op fn args)
  {constant (fn @(for each (({constant value} args))
                   value))})


;; Unparse.

(to (unparse-def {def name formals meaning})
  (may meaning
    (be {fun body} `(defun ,name ,formals ,(unparse-e body)))
    (be {op fn}    `(defop ,name))
    (be {thm body} `(dethm ,name ,formals ,(unparse-e body)))))

(to (unparse-e e)
  (may e
    (be {constant datum} `',datum)
    (be {variable name}  name)
    (be {if q a e}       `(if ,(unparse-e q)
                              ,(unparse-e a)
                              ,(unparse-e e)))
    (be {call name es}   `(,name ,@(each unparse-e es)))))


;; Parse.

(to (parse-proof xproof)
  (may xproof
    (be `(,xdef ,xseed ,@xsteps)
      (surely (not xseed))
      {proof (parse-def xdef)
             (parse-steps xsteps)})))

(to (parse-steps xsteps)
  (surely (list? xsteps) "Steps syntax")
  (each parse-step xsteps))

(to (parse-step xstep)
  (may xstep
    (be `(,xpath ,xe)
      ;; TODO check path well-formed
      ;; TODO check that xe is a call? I think?
      {step xpath (parse-e xe)})))

(to (parse-def xdef)
  (may xdef
    (be `(defun ,(? symbol? name) ,(? formals? formals) ,body)
      {def name formals {fun (parse-e body)}})
    (be `(dethm ,(? symbol? name) ,(? formals? formals) ,body)
      {def name formals {thm (parse-e body)}})
    (be `(,(? symbol? name) ,(? formals? formals) ,fn)
      {def name formals {op fn}})))

(to (formals? x)
  (and (list? x)
       (every symbol? x)
       (set? x)))

(to (parse-e xe)
  (may xe
    (be (? number?)     {constant xe})
    (be (? claim?)      {constant xe})
    (be ''nil           no-c)
    (be ''t             yes-c)
    (be `',datum        {constant datum})
    (be (? symbol?)     {variable xe})
    (be `(if ,x1 ,x2 ,x3)         {if (parse-e x1) (parse-e x2) (parse-e x3)})
    (be `(,(? symbol? f) ,@xargs) {call f (each parse-e xargs)})))


;; Lists as sets with order.
;; TODO use a map once we have them preserving insertion order

(to (append xs x)
  (if (xs .find? x)
      xs
      (chain xs `(,x))))

(to (set? xs)
  (= xs.count xs.range.count))
