;; (Use run.cant to run this.)

(import (use 'grid-2d)
  grid-2d<-)

(let input (with-input-file ~.read-lines data-file))

(let parse
  (like ":anyone '=' :integer ', ' :anyone '=' :integer '..' :integer"))

(let inputs (each parse input))


(let spring-x 500)
(let spring-y   0)
(let spring (~ spring-x spring-y))

(let clay-spots
  (for gather ((input inputs))
    (may input
      (be `("x" ,x "y" ,lo ,hi) (grid* `(,x) (lo .thru hi)))
      (be `("y" ,y "x" ,lo ,hi) (grid* (lo .thru hi) `(,y))))))

(let `((,xl0 ,yl) (,xh0 ,yh)) (bounds<- clay-spots))

;; Extend the x-bounds by 1 each way to allow for flow off the sides.
(let xl xl0.-)
(let xh xh0.+)

(let grid (grid-2d<- (~ xl yl) (~ xh yh) {constant #no}))
(for each! ((p clay-spots))
  (grid .set! (tuple<-list p) #\#))

(to (escaped? (~ _ y))
  (< yh y))

(to (clay? p)
  (= (grid .get p) #\#))

(to (show-map)
  (for each! ((y (spring-y .till yl)))
    (for each! ((x (xl .thru xh))) ;XXX exclusive range on ys and inclusive on xs? Is that right?
      (let p (~ x y))
      (out .display (if (= spring p) #\+ #\.)))
    out.newline)
  (grid .show (on (runes)
                (for each! ((rune runes))
                  (out .display (or rune #\.)))
                out.newline))
  out.newline)

;; (show-map)

;; TODO redo vector+
;;(to (under p)
;;  (vector+ p '(0 1)))
(to (under (~ x y))
  (~ x y.+))

(to (flood)
  (let under-spring (~ spring-x yl))
  (surely (<= spring-y yl))
  (surely (not (clay? under-spring)))  ;; gonna assume this for simplicity
  (flooding under-spring))

(to (blocked? p)
  ("#~" .find? (grid .get p)))

(to (flooding p)
  (unless (escaped? p)
    (surely (not (grid .get p)) "Only flood into openings")
    (grid .set! p #\|)
    (let below (under p))
    (unless (grid .get below)
      (flooding below))
    (when (blocked? below)
      ;; Spill to the sides.
      (let (~ x0 y0) p)
      (to (spilling x dx)
        (let x1 (+ x dx))
        (let next (~ x1 y0))
        (may (grid .get next)
          (be #no
            (grid .set! next #\|)
            (when (not (grid .get (under next)))
              (flooding (under next)))
            (if (blocked? (under next))
                (spilling x1 dx)
                x))
          (be #\| x)
          (be #\# x)
          (be #\~ (surely #no "Can't happen, can it?"))
          (else   (surely #no "Even less happenable"))))
      (let x-min (spilling x0 -1))
      (let x-max (spilling x0 1))
      (let span (for each ((x (x-min .thru x-max)))
                  (~ x y0)))
      (when (and (every (compose blocked? under) span)  ;; TODO I think this logic is now performed above
                 (blocked? (~ x-min.- y0))
                 (blocked? (~ x-max.+ y0)))
        (for each! ((spot span))
          (surely (= #\| (grid .get spot)) "Converting flowing to still water")
          (grid .set! spot #\~))))))


(do
  (flood)
  (let bag (bag<- grid.values))
  (out .say "Total water (part 1): ~w\n" (+ (bag #\~) (bag #\|)))
  (out .say "Still water (part 2): ~w\n" (bag #\~)))
