;; (Use run.cant to run this.)

(let input (with-input-file ~.read-lines data-file))
(let inputs (each ~.number input))

(to (part1)
  (for some ((i (25 .till inputs.count)))
    (let n (inputs i))
    (let ns (inputs .slice (- i 25) i))
    (and (not (for some ((`(,a ,b) (ns .combinations 2)))
                (= (+ a b) n)))
         n)))

(to (part2-naive) ; naive, adj.: the way I actually did it.
  (let target 258585477)                ;from part1
  (let sum-to (running-sum inputs))
  (for some ((i inputs.keys))
    (for some ((j ((+ i 2) .thru inputs.count)))
      (and (= target (- (sum-to j) (sum-to i)))
           (do (let slice (inputs .slice i j))
               (+ (min @slice) (max @slice)))))))

(to (running-sum xs)
  (~.array (link 0 (scanl + 0 xs))))

;; Way faster, but I didn't think of it:
(to (part2)
  (let target 258585477)                ;from part1
  (begin searching ((i 0) (j 0) (total 0))
    ;; Loop invariant (assuming all the inputs are nonnegative):
    ;;   total = sum(inputs[i:j])
    ;;   Either total < target or it's the smallest sum,
    ;;   starting at i, that's >= target.
    (may (compare target total)
      (be -1 (searching i.+ j (- total (inputs i))))
      (be 1  (searching i j.+ (+ total (inputs j))))
      (be 0  (hm (when (< (- j i) 2)
                   (searching i j.+ (+ total (inputs j))))
                 (else
                   (let slice (inputs .slice i j))
                   (+ (min @slice) (max @slice))))))))
