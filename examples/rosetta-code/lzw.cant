;; https://rosettacode.org/wiki/LZW_compression
;; "The Lempel-Ziv-Welch (LZW) algorithm provides loss-less data compression."

(to (show-lzw text)
  (for each ((code (encode text)))
    (if (<= code 255)
        (text<- (char<- code))
        code)))

(let default-codebook (each text<- ((char<- 0) .span 256)))

(to (encode text @(optional codebook))
  (let codes ((or codebook default-codebook) .inverse))
  (let result (flexarray<-))
  (let chunk
    (for foldl ((chunk "") (c text))
      (let t1 (text<- c))
      (let chunk-1 (chain chunk t1))
      (if (codes .maps? chunk-1)
          chunk-1
          (do (result .push! (codes chunk))
              (codes .set! chunk-1 codes.count)
              t1))))
  (unless chunk.none?
    (result .push! (codes chunk)))
  result.values)

(to (decode codes @(optional codebook))
  ("" .join (chunked-decode codes (or codebook default-codebook))))

;; TODO ugly!
(to (chunked-decode codes codebook)
  (if codes.none?
      '()
      (do
        (let chunks (flexarray<-list codebook))
        (let output (flexarray<-))
        (begin decoding ((chunk (chunks codes.first))
                         (codes codes.rest)) ;TODO not a great idea, reusing the name
          (output .push! chunk)
          (if codes.none?
              output.values
              (do (let code codes.first)
                  (chunks .push! chunk)
                  (let new-chunk (chunks code))
                  (chunks .set! chunks.count.-
                          (chain chunk (text<- new-chunk.first)))
                  (decoding (chunks code) codes.rest)))))))

(out .print (show-lzw "TOBEORNOTTOBEORTOBEORNOT"))

(let m "XXXXXXXXXXXX")
(out .print (show-lzw m))
(let x (encode m))
(out .print (chunked-decode x default-codebook))

(surely (= m (decode x)))

(let p2 "TOBEORNOTTOBEORTOBEORNOT")
(let m2 (encode p2))
(let x2 (decode m2))
(surely (= x2 p2))
