;; Text Register Machine interpreter
;; Ported from github.com/darius/sketchbook (trm.py)
;; http://www.indiana.edu/~iulg/trm/
;; Glossary:
;;   pc    program counter
;;   insn  instruction
;;   n     argument part of instruction
;;   reg   register

(import (use 'parson) like)

(to (trm-parse program)
  (-> program parse-program (: (each insn<- it)) ~.array))

(let parse-program (like "[{'1'+} {'#' '#'? '#'? '#'? '#'?} :tuple]*"))

(to (insn<- (~ ones hashes))
  {insn (insn-table hashes.count) ones.count})

(to (regs<- @texts)
  (array<- #no @texts))

(to (run insns regs @(optional loud?))
  (begin stepping [(pc 0)]
    (when (insns .maps? pc)
      (when loud?
        (show insns pc regs)
        out.newline)
      (let {insn fn n} (insns pc))
      (let d (fn n regs))
      (surely (not= d 0))
      (stepping (+ pc d))))
  regs)

(let insn-table
  (array<-
   'illegal-insn
   (make _
     (to ~.name "add-1")
     (to (~ n regs)
       (regs .set! n (chain (regs n) "1"))
       1))
   (make _
     (to ~.name "add-#")
     (to (~ n regs)
       (regs .set! n (chain (regs n) "#"))
       1))
   (make _
     (to ~.name "forward")
     (to (~ n regs) n))
   (make _
     (to ~.name "backward")
     (to (~ n regs) (- n)))
   (make _
     (to ~.name "case")
     (to (~ n regs)
       (may (regs .get n "")           ;TODO how about just (regs n)?
         (be "" 1)
         (be text
           (regs .set! n (text .from 1))
           (may (text 0)
             (be #\1 2)
             (be #\# 3))))))))

;; (import (use 'pretty-layout) ...)
;;XXX use me

;; Output an 'assembly listing' with optional machine state.
(to (show insns @(optional ?pc ?regs))
  (let pc   (or ?pc   0))
  (let regs (or ?regs (regs<-)))
  (let left (for each [((~ addr {insn fn n}) insns.items)]
              (let show-addr (if (= addr pc)
                                 "   "
                                 ("~3w" .format (~.magnitude (- pc addr)))))
              ("~d ~d ~w" .format show-addr fn.name n)))
  (let right (for each [((~ i text) regs.items.rest)]
               ("\tr~w: ~d" .format i text)))
  (out .display-lines (abut left right)))

;; Combine two lists of lines into one, combined horizontally
;; so that lines1 goes to the left of lines2, not above it.
(to (abut lines1 lines2)
  (flip (chain (flip lines1)
               (flip lines2))))

(to (flip texts)
  (each (: it.parts.text) (zip .padded #\space @texts)))

;; Smoke test
(to (main _)
  (show (trm-parse "1#111##"))
  out.newline

  (let move-r2-r1 (trm-parse "11#####111111###111###1##1111####1#111111####"))
  (let regs (regs<- "" "1#1#11##"))
  (show move-r2-r1 0 regs)
  (run move-r2-r1 regs)
  (out .print (regs .from 1)))


(export trm-parse regs<- run show main)
