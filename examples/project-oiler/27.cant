;; Quadratic primes

;; Considering n^2 + an + b where integer |a| < 1000, |b| <= 1000,
;; find a and b that produce the maximum number of primes for consecutive values of n from 0 up.

(import (use 'factoring) prime? list-primes-till)

(to (answer limit)
  (let ab-pair (best-pair-within limit))
  (let primes (initial-primes (poly-values (quadratic @ab-pair))))
  (out .say "~w yields ~w primes: ~w\n" ab-pair primes.count primes)
  (out .print (* @ab-pair)))

(to (best-pair-within limit)
  (let b-candidates (list-primes-till limit.+)) ; b must be prime because for n=0 the quadratic's value =b
  (for max-by ((pair (grid* ((- limit.-) .till limit) b-candidates)))
    (~.count (initial-primes (poly-values (quadratic @pair))))))

(to (initial-primes ns)
  (for keep-while ((n ns))
    (and (< 0 n) (prime? n))))

(to (poly-values f)
  (each/lazy f (0 .march)))

(to ((quadratic a b) n)
  (+ b (* n (+ a n))))

(answer 1000) ;; -59231
