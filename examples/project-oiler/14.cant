;; Longest Collatz sequence

(to (collatz-step n)
  (hm (if n.even? (n .quotient 2))
      (else (+ (* 3 n) 1))))

;; Trivial logic, but it'll be slow if we take this up to a million:

(to (collatz-chain n)
  (keep-until (:: (= it 1)) (iterating collatz-step n)))

;; So let's memoize.

(import (use 'memoize) memoize)

(let collatz-length
  (memoize (make _
             (to (~ 1) 0)
             (to (~ n) (+ 1 (collatz-length (collatz-step n)))))))

(for each! [(n (1 .till 10))]  ;; Test it.
  (surely (= (collatz-length n) ((collatz-chain n) .count))))

(out .print (max-by collatz-length (1 .till 1000000)))
