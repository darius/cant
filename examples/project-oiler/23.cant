;; Non-abundant sums

(import (use 'factoring) list-divisors)

;; "It can be shown that all integers greater than 28123 can be
;; written as the sum of two abundant numbers."
(let limit 28123)

(to (answer)
  (sum (those not-sum-of-2-abundants? (1 .thru limit))))

(to (not-sum-of-2-abundants? n)
  (not (small-abundant-sums .maps? n)))

(to (abundant? n)
  (< n (sum-proper-divisors n)))

(to (sum-proper-divisors n)
  (- (sum (list-divisors n)) n))

(let small-abundants (those abundant? (1 .till limit)))

;; Faster version of (those (:: (<= it limit)) (each sum (grid* small-abundants small-abundants)))
(to (list-sums)
  (let sums (flexarray<-))
  (let ks (array<-list small-abundants))
  (for each! ((i ks.keys))
    (let k (ks i))
    (begin summing ((j 0))
      (let kk (+ k (ks j)))
      (when (<= kk limit)
        (sums .push! kk)
        (when (< j i)
          (summing j.+)))))
  sums.values)

(let small-abundant-sums (set<-list (list-sums)))

(out .print (answer)) ;; 4179871
;; TODO Almost 10 minutes still.
