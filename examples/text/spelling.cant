;; Norvig's (simpler) spelling corrector
;; http://norvig.com/spell-correct.html

(import (use 'parson) like)

;; Try to find a word in `lexicon` that's similar to `word`.
;; Prefer the most common word with the fewest edits.
(to (correct word)
  (or (pick-known-word `(,word))
      (pick-known-word (let neighbors1 (edits1 word)))
      (pick-known-word (gather edits1 neighbors1))
      word))

(to (pick-known-word candidates)
  (let best (max-by lexicon candidates))
  (and (lexicon .maps? best) best))

(to (edits1 word)
  (let splits     (for each ((i (0 .thru word.count)))
                    (~ (word .slice 0 i)
                       (word .slice i))))
  (let inserts    (for gather (((~ a b) splits))
                    (for each ((c alphabet))
                      (chain a c b))))
  (let del-splits (for those (((~ a b) splits))
                    b.some?))
  (let deletes    (for each (((~ a b) del-splits))
                    (chain a (b .slice 1))))
  (let replaces   (for gather (((~ a b) del-splits))
                    (for each ((c alphabet))
                      (chain a c (b .slice 1)))))
  (let swaps      (for yeahs (((~ a b) del-splits))
                    (and (< 1 b.count)
                         (chain a (text<- (b 1) (b 0)) (b .slice 2)))))
  (uniquify (chain inserts deletes replaces swaps)))

(to (uniquify xs) xs.range.keys)

(let alphabet (each text<- (#\a .thru #\z)))

(to (words<-text text) (scan-words text.lowercase))
(let scan-words (like "({:letter+} | :skip)*"))

(let lexicon
  (bag<- (words<-text (with-input-file ~.read-all "examples/text/spelling.train.text"))))

(to (main _)
  (for each! ((word (words<-text "a lowsy spelur zzz")))
    (out .print (correct word))))
