;; How fast do you type?

(import ((use 'sturm) (export console))
  cbreak-mode render cursor get-key)

(to (main _)
  (cbreak-mode interact))

(to (interact)
  (show 0 "(Start typing...)")
  (let strokes (flexarray<- (get-key)))
  (let start (nano-now))
  (begin typing ()
    (show (/ (- (nano-now) start) 1000000000) strokes.text)
    ;; XXX polling/sleeping with in.ready? sucks, but it's
    ;; supported by the underlying Scheme.
    (may (and in.ready? (get-key))
      (be 'esc
        'done)
      (be ?key
        (may ?key
          (be #no        (nanosleep 50000000))  ; 1/20 sec
          (be 'backspace (unless strokes.none? strokes.pop!))
          (be (? rune?)  (strokes .push! ?key))
          (else))
        (typing)))))

(to (show t body)
  (let cps (if (or (= t 0) body.none?)
               0
               (/ body.count.- t)))
  (let wpm (/ (* cps 60) 5))
  (render [("~w seconds  ~w words/minute" .format t.floor wpm.floor)
           "   (Hit control-X to quit.)\n\n"
           body cursor]))
