;; A UI for cryptogram puzzles.
;; Ported from github.com/darius/sturm.
;; (Hit control-X to quit.)

(import ((use 'sturm) (export console))
  cbreak-mode get-key render
  cursor green red unstyled)
(import (use 'random)
  random-rng<-)

(to (main args)
  (let cryptogram
    (may args.rest
      (be '()     (random-encrypt (random-rng<- system-randomness-source<-)
                                  (run-fortune)))
      (be `(,str) str)
      (else       (error ("Usage: ~d [cryptogram]" .format (args 0))))))
  (for cbreak-mode ()
    (puzzle cryptogram)))

(let alphabet (#\a .to #\z))

(to (random-encrypt rng text)
  (let shuffled (hey (array<-list alphabet)
                     (-> (rng .shuffle! it))))
  (let code (map<-items (zip alphabet shuffled)))
  (string<-list (for each ((ch text.lowercase))
                  (code .get ch ch))))

(to (run-fortune)
  ;; TODO ensure fits in sturm's width
  (shell-run "exec fortune"))

(to (shell-run command)                 ;; TODO extract to a library
  (let `(,from-stdout ,to-stdin ,pid) (open-subprocess command))
  ;; TODO do we have to wait for it to terminate?
  ;; XXX catch subprocess errors
  from-stdout.read-all)

(to (puzzle cryptogram)
  (let cv (cryptoview<- cryptogram))
  (begin playing ()
    (render (cv .view #yes))
    (may (get-key)
      (be 'esc (render (cv .view #no)))
      (be key
        (may key
          (be 'home      cv.go-to-start)
          (be 'end       cv.go-to-end)
          (be 'left      (cv .shift-by -1))
          (be 'right     (cv .shift-by  1))
          (be 'up        (cv .shift-line -1))
          (be 'down      (cv .shift-line  1))
          (be 'shift-tab (cv .shift-to-space -1))
          (be #\tab      (cv .shift-to-space  1))
          (be 'backspace (cv .shift-by -1)
                         (cv .jot #\space))
          (be 'del       (cv .jot #\space)
                         (cv .shift-by 1))
          (be (and (? char?) (? ~.uppercase?))
                         (cv .shift-to-code 1 key))
          (else          (when (or (= key #\space) (alphabet .find? key))
                           (cv .jot key)
                           (cv .shift-by 1))))
        (playing)))))

;; The puzzle state.
(to (cryptoview<- cryptogram)

  (let code (those ~.letter? cryptogram.uppercase))
  (surely code.some?)            ;XXX 'require' or something
  (let decoder (!map-by (-> #\space) code.range.keys))
  (let point (box<- 0))                ; Index in `code` of the cursor

  (let lines (each clean cryptogram.uppercase.split-lines))
  (let line-starts                ; Set of positions that start a line
    (~.range (running-sum (for each ((line lines))
                            (tally-by ~.letter? line)))))

  (to (shift-by offset)
    (point .^= ((+ point.^ offset) .modulo code.count)))

  (to (shift-till offset stop?)
    (shift-by offset)
    (unless (stop?)
      (shift-till offset stop?)))

  (make _

    (to (~ .jot letter)
      (decoder .set! (code point.^) letter))

    (to ~.go-to-start
      (point .^= 0))

    (to ~.go-to-end
      (point .^= code.count.-))

    (to (~ .shift-by offset)
      (shift-by offset))

    (to (~ .shift-line offset)
      (shift-till offset (: (line-starts .maps? point.^))))

    (to (~ .shift-to-space offset)
      (when (decoder .find? #\space)
        (shift-till offset (: (= #\space (decoder (code point.^)))))))

    (to (~ .shift-to-code offset letter)
      (when (code .find? letter)
        (shift-till offset (: (= letter (code point.^))))))

    ;; Return a Sturm view of the decoding-in-progress.
    (to (~ .view show-cursor?)
      (let counts (hey (bag<- decoder.values) ; bag of decoded plaintext letters
                       (~ .delete! #\space)))
      (let clashes (~.range (for where ((n counts)) ; set of letters with >1 preimage
                              (< 1 n))))
      (let letters-left (for each ((ch alphabet)) ; display of letters available to try
                          (if (counts .maps? ch) #\space ch)))

      (let pos (box<- 0)) ; current index into `code`

      (let view (flexarray<-))
      (to (emit x) (view .push! x))

      (emit (green ["Free: " letters-left "\n"]))
      (for each! ((line lines))
        (emit #\newline)
        ;; a line of the tentative plaintext:
        (for each! ((ch line))
          (when (and show-cursor? ch.letter?)
            (when (= pos.^ point.^)
              (emit cursor))
            pos.+!)
          (emit (decoder .get ch ch)))
        (emit #\newline)
        ;; separator line:
        (for each! ((ch line))
          (emit (if ch.letter? #\- #\space)))
        (emit #\newline)
        ;; line of ciphertext:
        (for each! ((ch line))
          (let color (hm (if (clashes .maps? (decoder .get ch)) red)
                         (if (= ch (code point.^))              green)
                         (else                                  unstyled)))
          (emit (color ch)))
        (emit #\newline))

      view.values)))

;; Expand tabs; blank out other control characters.
(to (clean str)
  (let r (flexarray<-))
  (for each! ((ch str))
    (hm (when (= ch #\tab)
          (begin padding ()
            (r .push! #\space)
            (unless (8 .divides? r.count)
              (padding))))
        (when (< ch #\space)
          (r .push! #\space))
        (else
          (r .push! ch))))
  r.values)

(to (running-sum numbers)
  (let sums (flexarray<- 0))
  (for each! ((n numbers))              ;TODO scanl
    (sums .push! (+ sums.last n)))
  sums)

(export main)
