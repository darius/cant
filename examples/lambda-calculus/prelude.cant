;; Prelude, built-ins

(to (build-prelude powers interpret invoke reply)
  (import powers out)

  (to (make-church<-claim lc-no)
    {primitive (on (lc-yes)
                 {primitive (given
                              (be #no  lc-no)
                              (be #yes lc-yes))})})

  (to (make-church<-count lc-zero)
    {primitive
     (on (lc-succ)
       (let counting
         {cps-function
          (on (n sequel)
            (may n
              (be 0
                (reply sequel lc-zero))
              (be (? count? n)
                (invoke counting n.-
                        {cps-sequel
                         (on (ch-n-)
                           (invoke lc-succ ch-n- sequel))}))))}))})

  (to (make-church<-list lc-nil)
    {primitive
     (on (lc-link)
       (let linking
         {cps-function
          (on (xs sequel)
            (may xs
              (be '()
                (reply sequel lc-nil))
              (be (link h t)
                (invoke lc-link h
                        {cps-sequel
                         (on (ch-link-h)
                           (invoke linking t
                                   {cps-sequel
                                    (on (ch-t)
                                      (invoke ch-link-h ch-t sequel))}))}))))}))})

  (let builtins-env
    {module
     (map<- (~ 'make-church<-claim {primitive make-church<-claim})

            (~ 'add1 {primitive ~.+})
            (~ 'make-church<-count {primitive make-church<-count})

            (~ 'cant-link {primitive (on (h)
                                       {primitive (-> (link h it))})})
            (~ 'make-church<-list {primitive make-church<-list})

            (~ 'display {primitive (-> (out .display it))})
            (~ 'error {primitive (to (lambda-error _)
                                   (error "Error in lambda-calculus program"))})
            )})

  (let prelude
    '(do 

       (to (no if-yes if-no) if-no)
       (to (yes if-yes if-no) if-yes)
       (let church<-claim (make-church<-claim no yes))
       (to (claim<-church p) (p #yes #no))

       (to (if pred y n) (pred y n))
       (to (and p q) (if p q no))
       (to (or p q) (if p yes q))
       (to (not1 p) (p no yes)) ;; Right? Wikipedia sez "only correct for normal order". Why?
       (to (not p a b) (p b a))  ;; What they say is right for applicative order.

       (to (zero f x) x)
       (to (succ n f x) (f (n f x)))
       (let church<-count (make-church<-count zero succ))
       (to (count<-church n) (n add1 0))

       (to (+ m) (m succ))
       (to (* m n) (m (+ n) zero))
       (to (expt m n) (n m))

       (to (nil if-link if-nil) if-nil)
       (to (link h t if-link if-nil) (if-link h (t if-link if-nil)))
       (let church<-list (make-church<-list nil link))
       (to (list<-church xs) (xs cant-link '()))

       (to (chain xs ys) (xs link ys))

       (to (compose f g x) (f (g x)))
       (to (itself x) x)

       (to (fix maker)
         (to (r recur) (maker ([x] (recur recur x))))
         (r r))

       (the-environment)))

  (interpret prelude builtins-env))

(export build-prelude)
