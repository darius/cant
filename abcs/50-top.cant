;; Top level: listener and command line

(let the-last-error (box<- #no))

(to (push-signal-handler handler)
  (let parent-handler the-signal-handler.^)
  (to (handler/wrapped k evil)
    ;; TODO might be cleaner with unwind-protect actions spelled out inline
    (unwind-protect
     (to (handler-thunk)
       (the-signal-handler .^= parent-handler)
       (handler k evil))
     (to (unwind-thunk)
       (the-signal-handler .^= handler/wrapped))))
  (the-signal-handler .^= handler/wrapped))

(to (with-signal-handler handler thunk)
  (let parent-handler the-signal-handler.^)
  (the-signal-handler .^= (on (k evil)
                            (the-signal-handler .^= parent-handler)
                            (handler k evil)))
  (let result (thunk))
  (the-signal-handler .^= parent-handler)
  result)

(to (unwind-protect try finally) ;TODO better name; reconcile/integrate with ejectors
  (let parent-handler the-signal-handler.^)
  (the-signal-handler .^= (to (unwind-protector k evil)
                            (the-signal-handler .^= parent-handler)
                            (finally)
                            (parent-handler k evil)))
  (let result (try))
  (the-signal-handler .^= parent-handler)
  (finally)
  result)

(to (fallback-signal-handler k evil)
  ;; XXX for some reason, the panic handler doesn't seem to be set
  ;; when we're in here. So, for now let's just set it:
  (the-signal-handler .^= panic)

  ;; N.B. we're trying to use only primitives here as far as possible:
  (out .display "Error within error! Evils:\n")
  (begin printing ((xs evil))
    (when xs.none? (os-exit 1))
    (out .write xs.first)
    (out .display #\newline)
    (printing xs.rest)))

(to (start-playing @(optional command-line-args))
  (import (use 'traceback) on-error-traceback)

  (let parent-handler the-signal-handler.^)

  (to (script-handler k evil)
    (the-signal-handler .^= parent-handler)
    (the-last-error .^= {error k evil})
    (on-error-traceback k evil))

  (let listener (listener<- main-interactive-setting script-handler))

  (may command-line-args
    (be #no (listener))
    (be '() (listener))
    (be `("-i" ,filename ,@_)
      (the-signal-handler .^= listener.signal-handler)
      (load-and-run filename command-line-args.rest)
      (listener))
    (be `(,filename ,@_)
      (the-signal-handler .^= script-handler)
      (load-and-run filename command-line-args))))

(to (load-and-run filename args)
  (load filename `(,filename)) ;TODO remove .cant extension
  (when (main-interactive-setting .binds? 'main)
    ((main-interactive-setting 'main) args)))

(to (listener<- setting signal-handler)

  (to (listener-handler k evil)
    (signal-handler k evil)
    (out .display "Enter (debug) for more.\n")
    (listener))

  ;; A separate function just to make the top of tracebacks cleaner.
  (to (print-result value)
    (unless (= value void)
      (out .print value))
    (listener))

  (make listener
    (to ~.signal-handler
      listener-handler)
    (to (~)
      (the-signal-handler .^= listener-handler)
      (out .display "-> ")
      (may (read)
        (be (? eof?) out.newline)
        (be sexpr (print-result (cant .play sexpr setting)))))))

(to (debug)
  (import (use 'debugger) inspect-continuation)
  (may the-last-error.^
    (be {error k evil}
      (inspect-continuation k))
    (else
      (out .display "No error to debug.\n"))))

(to (breakpoint @values)
  ;; TODO automatically invoke the debugger
  (error "Breakpoint" @values))

(full-powered-setting .resolve-each! primordial-setting full-powered-setting-extra-names)

(import (use 'flexarray)  flexarray<- flexarray<-list)
(import (use 'sort)       sort sort-by)
(import (use 'bag)        bag<-)

(the-signal-handler .^= fallback-signal-handler)

(computational-setting .resolve-each! primordial-setting computational-setting-library-imports)
(full-powered-setting .resolve-each! primordial-setting computational-setting-library-imports)
