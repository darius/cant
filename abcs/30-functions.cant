;; Misc functions I want to be generally available without needing an import.
;; Some such functions are defined at an earlier stage in abcs/ because they're
;; used by other earlier-stage code.

;; (those ~.even? '(3 1 4 1 5 9 2 6)) = '(4 2 6)
;; No {extending zipped-list-transducer} for this one, because the
;; one-arg case is not substitutable for the n-arg: the n-arg version
;; would return a list of n-tuples, but a 1-tuple of x is not equal to
;; x itself. I rather wish it were, but 1-tuples seem to need to be
;; distinct for the sake of message dispatch.
(to (those keep? xs)
  (for foldr ((x xs) (kept '()))
    (if (keep? x) (link x kept) kept)))

;; (yeahs maybe xs) = a list of the non-#no results of (maybe x) for x in xs,
;; in order.
;;TODO is this worth defining? good name?
;;TODO this n-ary case is usually an error, complain? -- (yeahs maybe)
(make yeahs {extending zipped-list-transducer}
  (to (~ maybe xs)
    ;; Inlining of (those itself (each maybe xs))
    (for foldr ((x xs) (kept '()))
      (may (maybe x)
        (be #no  kept)
        (be yeah (link yeah kept))))))

;; The identity function.
(to (itself x)
  x)

;; (zip xs ys zs ...) = a list of (~ x y z ...) for x in xs and
;; corresponding y in ys, etc. The inputs must all have the same
;; length.
(make zip
  (to (~ xs ys)                           ;specialized for speed
    (to (mismatch)
      (oops "zip: mismatched arguments" xs ys))
    (begin zipping ((xs xs) (ys ys))
      (hm (if xs.none? (if ys.none? '() (mismatch)))
          (if ys.none? (mismatch))
          (else (link (~ xs.first ys.first)
                      (zipping xs.rest ys.rest))))))
  (to (~ @lists)
    (each tuple<-list (transpose lists)))
  ;; Like (zip @lists), but clipping the lists to the length of the shortest:
  ;; TODO move .ragged to zipped-list-transducer? and define zip in terms of it?
  (to (~ .ragged @lists) ;; TODO better name .clip ? nah...
    (begin zipping ((rows lists))
      (if (some ~.none? rows)
          '()
          (link (tuple<-list (each ~.first rows))
                (zipping (each ~.rest rows))))))
  ;; Like (zip @lists), but right-padding the lists to match the longest:
  (to (~ .padded padding @lists)
    (begin zipping ((rows lists))
      (if (every ~.none? rows)
          '()
          (link (each (~ .get 0 padding) rows)
                (zipping (each (~ .slice 1) rows)))))))

;; XXX float contagion
(make min {extending left-folding-operation}
  (to (~ a b) (if (< a b) a b)))
(make max {extending left-folding-operation}
  (to (~ a b) (if (< a b) b a)))

;; `(min-by f xs)` = an `x` in `xs` giving the minimum value of `(f x)`.
(to (min-by key<- xs)
  ;; The simplest code (foldr1 (on (x y) (if (< (key<- x) (key<- y)) x y)) xs))
  ;; can be painfully slow, from repeated calls to key<- on the best x so far.
  (surely xs.some?)
  (let x0 xs.first)
  (let (~ best-x _) (for foldl ((best-so-far (~ x0 (key<- x0)))
                                (x xs.rest))
                      (let (~ _ bsf-key) best-so-far)
                      (let xkey (key<- x))
                      (if (< xkey bsf-key) (~ x xkey) best-so-far)))
  best-x)

(to (max-by key<- xs)
  (surely xs.some?)
  (let x0 xs.first)
  (let (~ best-x _) (for foldl ((best-so-far (~ x0 (key<- x0)))
                                (x xs.rest))
                      (let (~ _ bsf-key) best-so-far)
                      (let xkey (key<- x))
                      (if (> xkey bsf-key) (~ x xkey) best-so-far)))
  best-x)

;; ((compound-key<- f g h) x) = (list<- (f x) (g x) (h x))
(to ((compound-key<- @key-fns) x)   ;; TODO shorter name? combo-key? call-each?
  (each (~ x) key-fns))

;; (where ok? map) = a list of those keys k of map for which (ok? (map k)).
;; Not sure this is the most useful design:
;;  - Most often we want a set, not a list (i.e. we want result.range)
;;  - Sometimes it's *almost* applicable, but ok? needs to take `(,i ,x) as the argument.
;;    But if we make that change, then it's barely different from yeahs.
(to (where ok? map)
  (for gather (((~ i x) map.items))
    (if (ok? x) `(,i) '())))

;; Make a map from k to (f k) for k in keys.
(to (map-by f keys) ;TODO maybe name it map<-keys ? along with a map<-values ?
  (map<-items (for each ((key keys))
                (~ key (f key)))))

(to (!map-by f keys) ;TODO ugly duplication
  (!map<-items (for each ((key keys))
                 (~ key (f key)))))

;; Make a map from (f v) to v for v in values.
(to (map<-values f values)
  (map<-items (for each ((value values))
                (~ (f value) value))))

(to (text-by f xs)
  ;; TODO zipped-list-transducer? (also for map-by etc.)
  ;; TODO also delete when (f x) = #no?
  ;;      (or make (#no .text) = ""? that's a bit much...)
  ;; TODO inconsistent to auto-.values here and not elsewhere, isn't it?
  ("" .join (each (:: ((f it) .text)) xs.values)))

;; List every way to pick one element from each of the arguments.
;; For example, (grid* '(a b) '(0 1)) = ((a 0) (a 1) (b 0) (b 1))
;; TODO return tuples instead?
;; What's a good name for this? I like 'cartesian*' even less.
;; 'combinations<-' maybe?
(make grid*
  (to (~)           '(()))
  (to (~ xs @lists) (for gather ((x xs))
                      (for each ((picks (grid* @lists)))
                        (link x picks)))))

(make grid*/lazy
  (to (~)           '(()))
  (to (~ xs @lists) (for gather/lazy ((x xs))
                      (for each/lazy ((picks (grid*/lazy @lists)))
                        (link x picks)))))

;; TODO zipped-list-transducer for the below?

(to (those/lazy keep? xs)
  (hm (if xs.none? '())
      (if (keep? xs.first)
          (link/lazy xs.first (: (those/lazy keep? xs.rest))))
      (else (those/lazy keep? xs.rest))))

(to (each/lazy f xs)
  (hm (if xs.none? '())
      (else (link/lazy (f xs.first)
                       (: (each/lazy f xs.rest))))))

(to (gather/lazy f xs)
  (for foldr/lazy ((x xs) (rest-thunk (: '())))
    (chain/lazy (f x) rest-thunk)))

(to (chain/lazy xs ys-thunk)
  (foldr/lazy link/lazy xs ys-thunk))

(to (foldr/lazy f xs z-thunk)
  (if xs.none?
      (z-thunk)
      (f xs.first
         (: (foldr/lazy f xs.rest z-thunk)))))

(to (yeahs/lazy maybe xs)
  (those/lazy itself (each/lazy maybe xs)))

(to (scanl/lazy f z xs)
  (begin scanning ((z z) (xs xs))
    (link/lazy z (: (if xs.none?
                        '()
                        (scanning (f z xs.first) xs.rest))))))

(to (scanl f z xs) ;; TODO better name
  (as-list (scanl/lazy f z xs)))

;; ((compose f g) x) = (f (g x)), etc.
(make compose {extending right-folding-operation}
  (to (~ f g)                   ;TODO support (to ((~ f g) @arguments)
    (on (@arguments)
      (f (g @arguments))))
  (to (~)
    itself))

;; (sum-by f '(a b)) = (+ (f 'a) (f 'b))
;;TODO overload 'sum' instead? (viz. other use of name below)
(make sum-by {extending zipped-list-transducer}
  (to (~ f xs)
    (for foldl ((total 0) (x xs))
      (+ total (f x)))))

(to (tally xs)
  (sum-by ~.count xs))

;; (tally-by f '(a b)) = (+ (~.count (f 'a)) (~.count (f 'b)))
(make tally-by {extending zipped-list-transducer}
  (to (~ f xs)
    (sum-by (compose ~.count f) xs)))
;; TODO hm, I was thinking of f as returning a claim, but as written,
;; it could be any function that returns a countable thing, such as a
;; collection. What's a good name for this from that point of view?
;; total-count ? total ? sum-by ? count-by ?

;; (-> input f g h) = (h (g (f input)))
;; Hm, (-> x) is just x, so this could also replace 'itself'...
;; is there a name suggestive of both functions? Maybe 'pass'?
(make -> {extending left-folding-operation}
  (to (~ input f)
    (f input)))

;; Like (f ?thing) but preserving #no-ness.
(to (mayhap f ?thing)
  (may ?thing
    (be #no #no)
    (else   (f ?thing))))

(to (with-input-file fn filename)
  (with-closing fn (open-input-file filename)))

(to (with-output-file fn filename)
  (with-closing fn (open-output-file filename 'replace))) ;TODO the 'replace is for Chez

(to (with-closing fn closable)
  (let result (fn closable))
  closable.close                       ;TODO unwind-protect
  result)

(to (read-all source) ;XXX confusing name, since source.read-all returns a text
  (begin reading ()
    (may (read source)
      (be (? zilch?) '())
      (be thing      (link thing (reading))))))

(to (system/must-succeed command)
  (unless (= 0 (system command))
    (oops "Failed system command" command)))

(to (setting<-map map)
  (__setting<- map.keys map.values.array #no))

(to (system-randomness-source<-)
  (open-input-file "/dev/urandom"))


;; Experiments

;; TODO is this the most useful version of this idea?
(to (iterate f x)
  (link/lazy x (: (iterate f (f x)))))

;; TODO too specialized for the stdlib
(to (union @sets)
  (hey (set<-) @(each (:: (~ .union! it)) sets)))

(to (intersect @sets)
  ;; TODO .intersect! like the above
  (foldl1 (on (x y) (x .intersect y))
          sets))

(to (take-while prefix? xs)             ;TODO as list/text method?
  (hm (if xs.none? '())
      (do (let x xs.first))
      (if (prefix? x)
          (link x (take-while prefix? xs.rest)))
      (else '())))
  
(to (drop-while prefix? xs)             ;TODO as list/text method?
  (if (and xs.some? (prefix? xs.first))
      (drop-while prefix? xs.rest)
      xs))
  
;; Split xs at its first element where split-point? is true.
;; That is, return `(,head ,tail), where (chain head tail) = xs,
;; and either tail is () or (split-point? tail.first) is true
;; at the first possible place.
;; TODO I forgot this existed
(to (split-on split-point? xs)
  (begin scanning ((r-head '()) (xs xs))
    (if (or xs.none? (split-point? xs.first))
        `(,(reverse r-head) ,xs)
        (scanning (link xs.first r-head) xs.rest))))

;; (intercalate T '(as bs cs)) = `(,@as ,T ,@bs ,T ,@cs)
;; XXX this code actually gives you (intercalate 'T '(a b c)) = '(a T b T c)
;; TODO almost never used
(to (intercalate between elements)      ;TODO unify with .join
  (hm (if elements.none? '())
      (else (link elements.first
                  (for foldr ((element elements.rest) (result '()))
                    (link between element result))))))

;; probably worthless
(to (method<- actor cue)
  (on (@arguments)
    (call actor (term<- cue arguments))))


;; Package it all up

;; TODO design a trace-log feature
;;(let trace-sink out)

(let computational-setting-names
     ;; TODO: nonrandom order
  '(computational-setting read-all cant
;;    trace-sink
    intercalate compound-key<- yeahs where split-on itself 
    those compose zip setting<-map
    link/lazier lazier<- 
    those/lazy yeahs/lazy grid*/lazy
    foldr/lazy gather chain/lazy 
    each/lazy gather/lazy link/lazy scanl/lazy iterate
    array-by map-by !map-by map<-values text-by method<- ->
    sum-by tally-by tally max min max-by min-by
    hey union intersect mayhap
    grid* sum = link link? list? number? integer? symbol? cue? ;TODO rename cue? or rm it
    claim? rune? array? box? term? source? sink? zilch? box<-
    text? number<-text text<-number
    list<-text text-source<- text-sink<-
    text<-list text<- text<-writer
    term<- rune<- array<-count inexact<-exact exact<-inexact
    not void /
    self-evaluating?
    maybe-macroexpand-expr maybe-macroexpand-patt
    setting? immutable-map? map<-items empty-setting
    oops with-ejector ejector-protect with-closing
    miranda-trait array-trait list-trait map-trait sink-trait
    as-list array<-list array<- map<- map<-lists tuple<-list yeah?
    list<- !map<- !map<-items !map<-lists set<- set<-list
    count? not= left-folding-operation right-folding-operation
    + * - transitive-comparison < <= <=> >= > compare
    gcd lcm
    chain reverse foldl foldl1 foldr foldr1 scanl transpose zipped-list-transducer
    each each! some every march<- take-while drop-while
    read
    assoc  ;; TODO remove
    __as-link __array<-list ;; used implicitly by parse expansion
    ))
(let computational-setting-library-imports
  '(use
    unwind-protect ;;TODO not exactly safe as currently defined
    sort-by sort flexarray<- flexarray<-list bag<-))
(let computational-setting
  (empty-setting .extend-promises
                 (chain computational-setting-library-imports
                        computational-setting-names)))
(computational-setting .resolve-each! primordial-setting computational-setting-names)
