(make-trait list-trait list
  (to (~ i)
    (may i
      (be 0 list.first)
      (else (list.rest i.-))))
  (to ~.none?
    (= 0 list.count)) ;N.B. these default implementations are circular
;  (to ~.first
;    (list 0))
  (to ~.rest
    (list .slice 1))
  (to ~.count
    ;; TODO non-tail-recursive would be more OO in style. Go back to that?
    (begin counting ((remainder list) (count 0))
      (if remainder.none?
          count
          (counting remainder.rest count.+))))
  (to (~ .chain seq)                         ;TODO self if seq is ()
    (if list.none?
        seq
        (link list.first (list.rest .chain seq))))
  (to (~ .slice i)
    (surely (<= 0 i))
    (hm (if (= i 0) list)
        (if list.none? list)
        (else (list.rest .slice i.-))))
  (to (~ .slice i bound)     ;XXX result is a link-list; be more generic?
    (surely (<= 0 i))
    (hm (if (<= bound i) '())
        (if list.none? '())
        (if (= i 0) (link list.first (list.rest .slice 0 bound.-)))
        (else (list.rest .slice i.- bound.-))))
  (to (~ .first-n n)  ; TODO better names?
    (list .slice 0 n))
  (to (~ .last-n n)   ; TODO better names? I'd like (~ .suffix n), except it suggests an index instead of a length
    (list .slice (- list.count n)))
  (to (~ .compare xs)
    ;; N.B. mutable arrays compare by this method, so it's really a comparison as of right now
    (hm (if list.none? (if xs.none? '= '<))
        (if xs.none? '>)
        (else (may (list.first .compare xs.first)
                (be '= (list.rest .compare xs.rest))
                (be d d)))))
  ;; A sequence is a kind of collection. Start implementing that:
  (to ~.keys
    (0 .till list.count)) ;TODO move this impl to array-trait; here, enum lazily.
  (to ~.values
    list)
  (to ~.items
    (enumerate list))
  (to (~ .get key default)
    (if (count? key)
        (begin walking ((k key) (xs list))
          (hm (if xs.none? default)
              (if (= k 0) xs.first)
              (else (walking k.- xs.rest))))
        default))
  (to (~ .maps? key)
    (and list.some?
         (or (= 0 key)
             (and (< 0 key)
                  (list.rest .maps? key.-)))))
  (to (~ .find value default)
    (begin looking ((i 0) (values list.values)) ;XXX .values called because list-trait is illegitimately used by arrays
      (hm (if values.none? default)
          (if (= value values.first) i)
          (else (looking i.+ values.rest)))))
  (to (~ .find value)
    (may (list .find value #no)
      (be #no (oops "Missing value" value))
      (be key key)))
  (to (~ .find? value)
    (may (list .find value #no)
      (be #no #no)
      (else   #yes)))
  (to (~ .last)
    (let rest list.rest)
    (if rest.none? list.first rest.last))
  (to (~ .but-last) ;; TODO better name?
    (let rest list.rest)
    (if rest.none? '() (link list.first rest.but-last)))
  (to (~ .prefix? p)
    (= (list .slice 0 p.count) p))   ;TODO more efficient
  (to (~ .repeat n)
    ;;TODO a method to get an empty seq of my type; and then factor out duplicate code
    (may n
      (be 0 '())             
      (else (chain @(for each ((_ n.till))
                      list)))))
  (to (~ .pad-right n padding)
    ;;TODO factor out duplicate code wrt text methods
    (let pad (- n list.count))
    (if (<= pad 0)
        list
        (chain list (`(,padding) .repeat pad))))
  (to (~ .pad-left n padding)
    (let pad (- n list.count))
    (if (<= pad 0)
        list
        (chain (`(,padding) .repeat pad) list)))
  (to ~.list
    (as-list list.values))
  (to ~.text
    (text<-list list.values))
  (to ~.array
    (array<-list list.values))
  (to ~.tuple
    (tuple<-list list.values))
  (to (~ .sample-key chaos)
    (chaos .sample-count list.count))
  (to (~ .alter alter!)                 ;; TODO an experiment
    (~.values (hey list.array alter!)))
  (to (~ .find-slice needle)
    ;; Return the index of the first slice in `list` equal to `needle`, or #no.
    ;;TODO interface like .find/.find?
    ;;TODO also a variant to start at a nonzero index
    (let P needle.count)
    (let j-limit (+ (- list.count P) 1))
    (begin sliding ((j 0))
      (and (< j j-limit)
           (begin checking ((i 0))
             (hm (if (= i P)
                     j)
                 (if (= (needle i) (list (+ i j)))   ;; TODO random access may be slow, quadratic
                     (checking i.+))
                 (else
                   (sliding j.+)))))))
  (to (~ .combinations n)   ;; Ways to pick n of my elements. TODO lazy, name, map-trait version?
    ;; TODO generate tuples instead of lists?
    (surely (count? n))
    (begin generating ((n n) (nxs list.count) (xs list))
      (hm (if (< nxs n) '())
          (if (= n 0)   '(()))
          ;; (if (= nxs n) (list<- xs))   ;; an optimization, but does it actually help?
          (else (chain (hide (let x xs.first)
                             (for each ((ys (generating n.- nxs.- xs.rest)))
                               (link x ys)))
                       (generating n nxs.- xs.rest))))))
  (to (~ .bag-combinations n) ;; Ways to pick n of my elements w/ duplicates allowed. TODO experimental
    (surely (count? n))
    (begin generating ((n n) (xs list))
      (hm (if (= n 0) '(()))
          (if xs.none? '())
          (else (chain (hide (let x xs.first)
                             (for each ((ys (generating n.- xs)))
                               (link x ys)))
                       (generating n xs.rest))))))
  (to ~.permutations
    (to (remove-nth xs n) ;; TODO a list method?
      (chain (xs .slice 0 n) (xs .slice n.+)))
    (begin permuting ((xs list))  ;; TODO lazy instead?
      (if xs.none?
          '(())
          (for gather (((~ i x) xs.items))
            (for each ((perm (permuting (remove-nth xs i))))
              (link x perm))))))
  (to (~ .permutations k)
    (for gather ((elements (list .combinations k)))
      elements.permutations))
  (to (~ .slices k)
    ;; TODO make k optional
    ;; TODO make it reasonably efficient without random access
    (for each ((i (0 .thru (- list.count k))))
      (list .slice i (+ i k))))
  (to (~ .k-tuples k)
    (grid* @(`(,list) .repeat k)))
  (to ~.cycle
    (surely list.some?)
    (begin cycling ((xs list))
      (if xs.none?
          (cycling list)
          (link/lazy xs.first (: (cycling xs.rest))))))
  (to (~ .maybe @(optional if-none))  ;; TODO an experiment TODO could be defined on maps in general too
    (if list.none?
        if-none
        (do (unless list.rest.none?
              (oops "Tried to convert to maybe from count >1" list))
            list.first)))
  (to ~.only  ;; TODO an experiment TODO could be defined on maps in general too, but then there's only-key vs. only-value
    (when list.none?
      (oops "Tried to .only from empty" list))
    (unless list.rest.none?
      (oops "Tried to .only from count >1" list))
    list.first)
  (to message
    (map-trait list message)))
