;; Parse regular expressions

(import (use 'parson) feed push grammar<-)

;; TODO: something like Python's raw string literals. \\\\ is awful.
(let grammar (grammar<- "
regex    :  exp :end.
exp      :  term ('|' exp     :either)*
         |                    :empty.
term     :  factor (term      :then)*.
factor   :  primary (  '*'    :star
                     | '+'    :plus
                     | '?'    :maybe
                    )?.
primary  :  '(' exp ')'
         |  '[' rune* ']'     :join :oneof
         |  '.'               :dot
         |  '\\\\' {1}        :literal
         |  !( '.' | '(' | ')'
             | '*' | '+' | '?'
             | '|' | '[' | ']')
            {1}               :literal.
rune     :  '\\\\' {1}
         |  !']' {1}.
"))

(to (regex-parser<- builder)
  (let semantics
    (map<- (~ 'empty   (push (builder 'empty)))
           (~ 'literal (feed (builder 'literal)))
           (~ 'star    (feed (builder 'star)))
           (~ 'then    (feed (builder 'then)))
           (~ 'either  (feed (builder 'either)))
           (~ 'plus    (feed (builder 'plus)))
           (~ 'maybe   (feed (builder 'maybe)))
           (~ 'oneof   (feed (builder 'one-of)))
           (~ 'dot     (push (builder 'anyone)))))
  (let parser ((grammar semantics) 'regex))
  (to (parse-regex text)
    ((parser .parse-outcome text) .result)))

(export regex-parser<-)
