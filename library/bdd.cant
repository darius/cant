;; Binary decision diagrams.
;; Explained (in Python) at
;; https://codewords.recurse.com/issues/four/the-language-of-choice

(make bdd-and {extend right-to-left-operation}
  (to (~ f g) (do-choose g lit0 f)) ;TODO rename do-choose
  (to (~) lit1))

(make bdd-or {extend right-to-left-operation}
  (to (~ f g) (do-choose g f lit1))
  (to (~) lit0))

(let lit0 0)
(let lit1 1)

(to (constant<- value)
  (surely (or (= value lit0) (= value lit1))
          "Not binary" value)
  value)

(let infinite-rank 0x7fffffff)  ;; (Greater than any variable's rank.)
(let ranks (flexarray<- infinite-rank infinite-rank))
(let if0s  (flexarray<- lit0 lit1))
(let if1s  (flexarray<- lit0 lit1))
(let ifs   [if0s if1s])

(to (dedup memo k1 k2 k3)
  (let mem1 (memo .get-set! k1 !map<-))
  (let mem2 (mem1 .get-set! k2 !map<-))
  (list<- (mem2 .get k3) mem2))

(let choice-memo (!map<-))

;; Given a variable numbered `rank` and two bdd nodes, return a bdd
;; node for the expression "if variable_rank = 0 then if0 else if1".
;; TODO document the preconditions
(to (build-choice rank if0 if1)
  (let `(,already ,memo-table) (dedup choice-memo rank if0 if1))
  (or already
      (do (surely (< rank infinite-rank))
          (let index (ranks .push! rank))
          (if0s .push! if0)
          (if1s .push! if1)
          (memo-table .set! if1 index)
          index)))

(to (make-choice rank if0 if1)
  (if (= if0 if1)
      if0
      (build-choice rank if0 if1)))

;; Return the value of `node` given the settings of its variables in `env`.
(to (bdd-evaluate node env)
  (if (<= node lit1)
      node
      (do (let value (env (ranks node)))
          (bdd-evaluate ((ifs value) node) env))))

(to (do-choose node if0 if1)
  (hm (if (<= node lit1)
          (may node
            (be 0 if0)                      ;N.B. 0 == lit0
            (be 1 if1)))
      ;; The next two cases usually save work, but aren't needed for
      ;; correctness.
      (if (= if0 if1)
          if0)
      (if (and (= if0 lit0) (= if1 lit1))
          node)
      (else
          (choose node if0 if1))))

(let choose-memo (!map<-))

;; Like `do-choose`, but McCarthy-standardized, presupposing the
;; arguments are all McCarthy-standardized.
(to (choose node if0 if1)
  (let `(,already ,memo-table) (dedup choose-memo node if0 if1))
  (or already
      (do (surely (< lit1 node))
          (let top (min (ranks node) (ranks if0) (ranks if1)))
          (let on0 (do-choose (subst top lit0 node)
                              (subst top lit0 if0)
                              (subst top lit0 if1)))
          (let on1 (do-choose (subst top lit1 node)
                              (subst top lit1 if0)
                              (subst top lit1 if1)))
          (let result (make-choice top on0 on1))
          (memo-table .set! if1 result)
          result)))

;; Specialize `node` to the case where variable number `rank` takes the
;; given value, `replacement`. Again, node must be standardized.
(to (subst rank replacement node)
  (may (rank .compare (ranks node))
    (be '< node)
    (be '= (do-choose replacement (if0s node) (if1s node)))
    (be '> (make-choice (ranks node)
                        (subst rank replacement (if0s node))
                        (subst rank replacement (if1s node))))))

;; Return the lexicographically first env which makes node evaluate to
;; goal, if possible; else #no.
(to (satisfy-first node goal)
  (let goal-node (constant<- goal))
  (let env (!map<-))
  (begin walking (node)
    (hm (when (<= node lit1)
          (and (= node goal-node) env))
        (let if0 (if0s node))
        (when (or (< lit1 if0) (= if0 goal-node))
          (env .set! (ranks node) 0)
          (walking if0))
        (else
          (env .set! (ranks node) 1)
          (walking (if1s node))))))


;; Conveniences

;; Return a BDD that evaluates to 1 just when every variable in map
;; has its given value.
;; TODO ugly inconsistency: this takes a map of variable -> bool
;;   while satisfy-first produces a map of variable -> bit
;;   Also naming: env vs. map
(to (match-map map)
  (for amass ((tree lit1)
              ((~ var value) (sort map.items {reverse})))
    (if value
        (build-choice var lit0 tree)
        (build-choice var tree lit0))))


(export
  satisfy-first build-choice bdd-and bdd-or lit0 lit1 constant<- bdd-evaluate
  match-map)
;; TODO export more, rename
;; XXX why is build-choice exported? well, you need to explicitly create the variable,
;;       though this interface sucks: 1. make-choice is better
;;                                    2. doesn't guarantee it's standardized
;;   why isn't `choose`? did I just get confused?
;;    (or rather 'do-choose', with but named 'choose'.)
