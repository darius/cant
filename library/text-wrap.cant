;; Like Python's textwrap.
;; TODO try using Parson
;; TODO the Python names are pretty arbitrary

;; Reformat text, trying to fit in width.
(to (fill text width)
  ("\n" .join (wrap text width)))

;; Reformat text into a list of lines, trying to fit in width.
(to (wrap text width)
  (surely (< 0 width))  ;TODO 'require' or something, for preconditions
  (wrap-into (flexarray<-) (parse-tokens text) width))

(to (parse-tokens text)
  (hm (if text.none? '())
      (may text.first
        (be #\newline (link {break} (parse-tokens text.rest)))
        (be #\space   (link {space} (parse-tokens text.rest)))
        (be (? ~.whitespace? rune)
          (oops "I don't know how to fill whitespace like" rune))
        (be rune
          (let word (flexarray<- rune))
          (do eating [(text text.rest)]
            (hm (if (or text.none? text.first.whitespace?)
                    (link {word word.text} (parse-tokens text)))
                (else (word .push! text.first)
                      (eating text.rest))))))))

(to (wrap-into line tokens width)
  (do scanning [tokens (spaces 0)]
    (hm (if tokens.none?
            (if line.none? '() `(,line.text)))
        (may tokens.first
          (be {space}
            (scanning tokens.rest spaces.+1))
          (be {break}
            (link line.text (wrap-into (flexarray<-) tokens.rest width)))
          (be {word w}
            (hm (when (<= (+ line.count spaces w.count) width)
                  (line .extend! (chain (" " .repeat spaces) w))
                  (scanning tokens.rest 0))
                (else
                  (link line.text
                        (wrap-into (flexarray<-list w) tokens.rest width)))))))))

(export fill wrap)
