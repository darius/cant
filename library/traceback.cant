;; An oops handler that prints a (crude) traceback.

;; We try to call on less of the library than usual, to work better
;; when the system is borked.
(import (use 'bounded-writer) bounded-writer<-)

(to (module powers)
  (import powers out)

  (to (on-oops-traceback k evil)
    (out .display "Oops! Traceback:\n")
    (print-traceback k)
    (complain evil))

  (to (on-oops-complain k evil)
    (out .display "Oops!\n")
    (complain evil))

  (to (print-traceback k)
    (for each! ((frame (reverse k)))
      (out .display "  ")
      (-> out (bounded-writer<- (~ .write frame) 77))
      (out .display "\n")))

  (to (complain evil)
    (may evil
      (be `(,(? text? plaint) ,@values)
        (out .display plaint)
        (out .display ": ")
        (-> out (bounded-writer<- (~ .write values) (* 80 20))))
      (else
        (out .display "Nonstandard evil: ")
        (out .write evil)))
    out.newline)

  (export on-oops-traceback on-oops-complain complain print-traceback))
