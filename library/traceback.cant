;; An error handler that prints a (crude) traceback.

;; We try to call on less of the library than usual, to work better
;; when the system is borked.
(import (use 'bounded-writer) bounded-writer<-)

(to (module powers)
  (import powers out)

  (to (on-error-traceback k evil)
    (out .display "Error! Traceback:\n")
    (print-traceback k)
    (complain evil))

  (to (on-error-complain k evil)
    (out .display "Error!\n")
    (complain evil))

  (to (print-traceback k)
    (for each! ((frame (reverse k)))
      (out .display "  ")
      (take out (bounded-writer<- (~ .write frame) 77))
      (out .display "\n")))

  (to (complain evil)
    (may evil
      (be `(,(? string? plaint) ,@values)
        (out .display plaint)
        (out .display ": ")
        (take out (bounded-writer<- (~ .write values) (* 80 20))))
      (else
        (out .display "Nonstandard evil: ")
        (out .write evil)))
    out.newline)

  (export on-error-traceback on-error-complain complain print-traceback))
