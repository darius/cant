;; Decision diagrams, generalizing bdd.cant to multiway branches.
;; Choice nodes select a branch by the value of a variable, from 0 to
;; #branches-1. Each variable is also represented by a number, its 'rank'
;; -- lower rank numbers are placed nearer the root of the tree.

(import (use 'memoize) memoize)

(let infinity 999999)  ;; N.B. we don't have floats yet

(make nil)  ;; Disjoint from all the values of constants.

(to (constant<- value)
  (surely (not= value nil) "That can't be a constant value -- it's reserved")
  (make
    (to ~.rank            infinity)
    (to ~.constant-value  value)
    (to (~ .evaluate env) value)
    (to (~ @nodes)        (nodes value))))

(to (choice<- rank branches)
  (surely (< rank infinity))
  (make choice
    (to ~.rank            rank)
    (to ~.constant-value  nil)
    (to (~ .evaluate env) ((branches (env rank)) .evaluate env))
    (to ~.branches        branches)
    (to (~ @nodes)        (hm (if nodes.unique?
                                  nodes.first)
                              (if (= nodes.keys.list (each ~.constant-value nodes))
                                  choice)
                              (else
                                  (memo-choice choice nodes))))))

(let memo-node (memoize choice<-))

(to (variable<- rank arity)
  (memo-node rank (each constant<- arity.till)))

(let memo-choice
  (memoize
   (on (node branches)
     (let top (min-by ~.rank (link node branches)))
     (let rank top.rank)
     (make-node rank
                (for each [(c top.branches.keys)]
                  ((subst rank c node) @(subst-each rank c branches)))))))

(to (make-node rank branches)
  (if branches.unique?
      branches.first
      (memo-node rank branches)))

(to (subst-each rank value nodes)
  (for each [(e nodes)]
    (subst rank value e)))

(to (subst rank value node)
  (may (rank .compare node.rank)
    (be '< node) ; N.B. node must be a constant, iff we arrived here (XXX why?)
    (be '= (node.branches value))
    (be '> (make-node node.rank
                      (subst-each rank value node.branches)))))

(to (valid? node)
  (not (satisfy node 0)))

(to (satisfy node goal)
  (let env (!map<-))
  (do walking [node]
    (if (not= nil node.constant-value)
        (and (= goal node.constant-value)
             env)
        (for fold$ [((~ value branch) node.branches.items)
                    (try-remaining-branches ($ #no))]
          (hm (when (`(,nil ,goal) .find? branch.constant-value)
                (env .set! node.rank value)
                (walking branch))
              (else
                (try-remaining-branches)))))))

(export constant<- variable<- satisfy valid?)
