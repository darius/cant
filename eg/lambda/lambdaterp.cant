;; Let's work out a source-level debugger in a simpler setting,
;; the call-by-value lambda calculus.
;; (That's the goal; not there yet.)

;; Conventions:
;;  lexp    source form of lambda-calculus expression
;;  c       constant value
;;  v       variable name (a symbol)
;;  r       environment
;;  k       continuation
;;  others  an AST or a value

(to (parse lexp)
  (may lexp
    (be (? symbol?)
      (var-ref<- lexp))
    (be (? number?)
      (constant<- lexp))
    (be `(lambda (,v) ,body)
      (abstraction<- v (parse body)))
    (be `(,operator ,operand)
      (call<- (parse operator)
              (parse operand)))))

(to (interpret lexp)
  ((parse lexp) .evaluate global-env halt))


;; ASTs and continuations

(make halt
  (to ~.none? #yes)                     ;TODO use list-trait for .some?, bleah.
  (to (~ .inject k<-) halt)
  (to (~ .take-step val) val)
  (to (~ .take val) val))


;; Constant
(to (constant<- c)
  (make constant
    (to ~.source           c)
    (to (~ .eval-step r k) (debugging (value-step<- constant r k)))
    (to (~ .evaluate r k)  (k .take c))))

;; Variable reference
(to (var-ref<- v)
  (make var-ref
    (to ~.source
      v)
    (to (~ .eval-step r k)
      (debugging (value-step<- var-ref r k)))
    (to (~ .evaluate r k)
      (lookup r v k))))

;; Lambda expression
(to (abstraction<- v body)
  (make abstraction
    (to ~.source
      `(& ,v ,body.source))
    (to (~ .eval-step r k)
      (debugging (value-step<- abstraction r k)))
    (to (~ .evaluate r k)
      (k .take (make _
                 (to ~.survey
                   `(,v -> <body>))
                 (to (~ .call arg k2)
                   (body .evaluate (extend r v arg) k2))
                 (to (~ .call-step arg k2)
                   (body .eval-step (extend r v arg) k2)))))))

;; Application
(to (call<- operator operand)
  (make app
    (to ~.source
      `(,operator.source ,operand.source))
    (to (~ .eval-step r k)
      (debugging (subeval-step<- operator r (ev-arg-cont<- operand r k))))
    (to (~ .evaluate r k)
      (operator .evaluate r (ev-arg-cont<- operand r k)))))

(to (ev-arg-cont<- operand r k)
  (make (to ~.none? #no)
        (to ~.rest  k)
        (to ~.first `(^ ,operand.source))
        (to (~ .inject k<-) (ev-arg-cont<- operand r (k<- k)))
        (to (~ .take fn)
          (operand .evaluate r (call-cont<- fn k)))
        (to (~ .take-step fn)
          (operand .eval-step r (call-cont<- fn k)))
        ))

(to (call-cont<- fn k)
  (make (to ~.none? #no)
        (to ~.rest  k)
        (to ~.first `(,(survey fn) ^))
        (to (~ .inject k<-) (call-cont<- fn (k<- k)))
        (to (~ .take arg)
          (fn .call arg k))
        (to (~ .take-step arg)
          (fn .call-step arg k))
        ))


;; Built-in values

(to (survey value)
  (if (or (number? value) (symbol? value))
      value
      value.survey))

(make prim+
  (to ~.survey '+)
  (to (~ .call-step arg1 k1)
    XXX)
  (to (~ .call arg1 k1)
    (if (number? arg1)
        (k1 .take (make _
                    (to ~.survey
                      `(+ ,(survey arg1)))
                    (to (~ .call-step arg2 k2)
                      XXX)
                    (to (~ .call arg2 k2)
                      (if (number? arg2)
                          (k2 .take (+ arg1 arg2))
                          ;; XXX should supply self, too:
                          (debug k2 "Bad arg2 to +" (survey arg2))))))
        (debug k1 "Bad arg1 to +" (survey arg1)))))


;; Environments

(let global-env
  `((+ ,prim+)))

(to (extend r v val)
  `((,v ,val) ,@r))

(to (lookup r v k)
  (may (assoc v r)
    (be #no    (debug k "Unbound var" v))
    (be record (k .take (record 1)))))


;; Debugger
;; Instead of interacting at a prompt, it takes a list of commands,
;; for now, for ease of rerunning during development.

(let command-queue (box<- '()))

(to (next-command)
  (display "debug> ")
  (may command-queue.^
    (be '()
      out.newline
      #no)
    (be `(,first ,@rest)
      (out .print first)
      (command-queue .^= rest)
      first)))

(to (debug k plaint irritant)
  (complain plaint irritant)
  (traceback k)
  (debugging (out-step<- k 'default-error-value)))

(to (complain plaint irritant)
  (out .say "Lambdaterp error: ~w: ~w\n" plaint irritant))

(to (traceback k)
  (each! (-> (out .print it)) k))

(to (debugging state)
  (let cmd (next-command))
  (if cmd (call state cmd) #no))

(to (value-step<- e r k)
  (make value-step
    (to ~.show
      (display "ev-> ") (out .print e.source))
    (to ~.b
      (traceback k)
      (debugging value-step))
    (to ~.continue
      (e .evaluate r k))
    (to ~.hop
      (e .evaluate r (k .inject debugger-trap-cont<-)))
    (to ~.step
      value-step.hop)
    ))

(to (subeval-step<- e r k)
  (make subeval-step
    (to ~.show
      (display "ev-> ") (out .print e.source))
    (to ~.b
      (traceback k)
      (debugging subeval-step))
    (to ~.continue
      (e .evaluate r k))
    (to ~.hop
      (e .evaluate r (k .inject debugger-trap-cont<-)))
    (to ~.step
      (e .eval-step r k))
    ))

(to (out-step<- k value)
  (make out-step
    (to ~.show
      (display "<-ret ") (out .print (survey value)))
    (to ~.b
      (traceback k)
      (debugging out-step))
    (to ~.continue
      (k .take value))
    (to ~.hop
      ((k .inject debugger-trap-cont<-) .take value))
    (to ~.step 
      (k .take-step value))
    (to (~ .value new-value)
      (debugging (out-step<- k new-value)))
    ))

(to (debugger-trap-cont<- k)
  (if (= k halt)
      k
      (make _
        (to (~ .take value) (debugging (out-step<- k value)))
        ;; XXX (.inject ...) ?
        (to message (call k message)))))


;; Smoke test

(hide

 (to (try lexp @commands)
   (command-queue .^= commands)
   (let result (interpret lexp))
   (if result (out .print (survey result)) 'failed))

 (try '(lambda (x) x))
 (try '((lambda (x) ((+ x) 2)) 1))

 (try '((lambda (x) ((+ y) 1)) 42))
 (try '((+ (lambda (z) z)) y))
 (try '(((+ 1) y) 2))

 (try '((lambda (x) ((+ y) 1)) 42)
      (~ .value 42)
      ~.continue)
 (try '((lambda (x) ((+ y) 1)) 42)
      (~ .value 42)
      ~.hop
      ~.b
      ~.hop)
)
