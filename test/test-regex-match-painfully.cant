(import (use 'memoize) memoize)

;; Given `size`, return a list of Cant expressions, of that size,
;; which produce regexes. (You have to play the expression in a
;; setting that has imported a regex module.) The size is the count of
;; regex constructors used.
(to (re-generator<- alphabet)
  (to ((tag name) @arguments)
    `(,name ,@arguments))
  (let gen
    (memoize (on (size)
               (let res (flexarray<-))
               (when (= 1 size)
                 (res .extend! (link 'empty (each (tag 'lit-rune) alphabet.values))))
               (when (<= 2 size)
                 (res .extend! (each (tag 'star) (gen size.-))))
               (when (<= 3 size)
                 (for each! ((i (1 .till size.-)))
                   (for each! ((`(,re1 ,re2) (grid* (gen i) (gen (- size i 1)))))
                     (res .push! ((tag 'either) re1 re2))
                     (res .push! ((tag 'then) re1 re2)))))
               res.values))))

;; Smoke test for the above generator:
;; (disabled)
'(hide
  (let g (re-generator<- "ab"))
  (each! (-> (out .print it)) (g 1))
  out.newline
  (each! (-> (out .print it)) (g 2))
  out.newline
  (each! (-> (out .print it)) (g 3)))

;; How many regexes of each size?
;; (disabled)
(to (tabulate alphabet)
  (let g (re-generator<- alphabet))
  (for each! ((size (1 .thru 8)))
    (out .say "~w ~w ~w\n" alphabet.count size (take size g ~.count))))
'(do
  (tabulate "01")  out.newline
  (tabulate "012") out.newline)

;; The main thing now: test regexes generated as above, against all
;; inputs up to an input size limit, with output judged according to
;; the regex-gen module.

(import (use 'regex-gen) regex-generate)
(import (use 'regex-match) regex-match)

(to (test-exhaustively alphabet re-limit input-limit)
  (let gen-setting   (computational-setting .extend-map (use 'regex-gen)))
  (let match-setting (computational-setting .extend-map (use 'regex-match)))
  (for each! ((re (gather (re-generator<- alphabet) (1 .thru re-limit))))
    (out .say "~w\n" re)
    (let rg (cant .play re gen-setting))
    (let rm (cant .play re match-setting))
    (for each! ((length (0 .thru input-limit)))
      (let all-inputs (each text<-list (grid* @(`(,alphabet) .repeat length))))
      (let should-match (regex-generate rg (set<- length)))
      (let should-not (take (all-inputs.range .difference should-match.range)
                            ~.keys sort))
      (out .say "~w Y: ~w N: ~w\n" length should-match should-not)
      (for each! ((inputs  `(,should-match ,should-not))
                  (should? '(#yes          #no)))
        (for each! ((input inputs))
          (unless (= should? (regex-match rm input))
            (out .say "  FAIL: ~w should~d match ~w\n" re (if should? "" " not") input)))))
    out.newline))

;;(test-exhaustively "ab" 6 3)
(test-exhaustively "ab" 2 2)  ; barely any, for quick automated 'testing'
